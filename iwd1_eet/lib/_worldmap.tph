<<<<<<<<.../wmp.l
>>>>>>>>

ACTION_DEFINE_ASSOCIATIVE_ARRAY wmptra BEGIN
//  areaname => dialog.tra number
	id1000 => ~@10709~
	id2000 => ~@19731~
	id2100 => ~@3061~
	id3000 => ~@10712~
	id3600 => ~@10921~
	id4000 => ~@19730~
	id5000 => ~@16234~
	id6000 => ~@22854~
	id7000 => ~@15663~
	id8001 => ~@29744~
	id9100 => ~@3069~
	id9200 => ~@10923~
	id9300 => ~@15757~
	id9500 => ~@19729~
	id9600 => ~@10925~
	id9700 => ~@100061~
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY mapicon BEGIN
//  folder => bam_anim
	icon => ~ZZZ~
END

// creates the worlmap.tra file from the wmptra array
COPY ~.../wmp.l~ ~%MOD_FOLDER%/lang/%LANGUAGE%/worldmap.tra~
ACTION_PHP_EACH ~wmptra~ AS area => strref BEGIN
	ACTION_BASH_FOR ~%MOD_FOLDER%/lang/%LANGUAGE%~ ~^.*\.tra$~ BEGIN
		ACTION_IF FILE_CONTAINS_EVALUATED (~%MOD_FOLDER%/lang/%LANGUAGE%/%BASH_FOR_FILE%~ ~^%strref%~) BEGIN
			COPY - ~%MOD_FOLDER%/lang/%LANGUAGE%/%BASH_FOR_FILE%~ ~%MOD_FOLDER%/lang/%LANGUAGE%~
				REPLACE_EVALUATE CASE_INSENSITIVE ~~~~~^[ %TAB%]*%strref%[ %TAB%]*[=][ %TAB%]*[\~"]\(.*\)["\~]~~~~~ BEGIN
					INNER_PATCH_SAVE string "%MATCH1%" BEGIN REPLACE_TEXTUALLY " " "###" END
				END ~%MATCH0%~
			APPEND_OUTER ~%MOD_FOLDER%/lang/%LANGUAGE%/worldmap.tra~ ~@%area% = "%string%"~
		END
	END
END

INCLUDE ~iwd1_eet/worldmap/icon/add_map_icons_ee.tpa~
ACTION_PHP_EACH ~mapicon~ AS folder => bam_anim BEGIN
	LAF ADD_MAP_ICONS_EE
	STR_VAR
		path_to_icons = EVAL ~%MOD_FOLDER%/Worldmap/%folder%/icon.bam~
		patch_to_pvrz = EVAL ~%MOD_FOLDER%/Worldmap/%folder%~
	RET
		new_icon_index
	END
	ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_icons BEGIN ~%bam_anim%~ => ~%new_icon_index%~ END
END

INCLUDE ~%MOD_FOLDER%/lib/add_worldmap_tbl.tpa~
LAF ADD_WORLDMAP_TBL
	INT_VAR
    	inclSv = 0
		patch_bp_files = 1
	STR_VAR
    	path_to_areas = EVAL ~%MOD_FOLDER%/Worldmap/areas.tbl~
    	path_to_links = EVAL ~%MOD_FOLDER%/Worldmap/links.tbl~
		path_to_areas_bp = EVAL ~%MOD_FOLDER%/Worldmap/areas-bp-bgt.tbl~
		path_to_links_bp = EVAL ~%MOD_FOLDER%/Worldmap/links-bp-bgt.tbl~
    	path_to_trans = EVAL ~%MOD_FOLDER%/lang/%LANGUAGE%/worldmap.tra~
		icons_table = ~remapped_icons~
END


















